PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lpviz: <http://visualization.linkedpipes.com/ontology/>

DELETE {
  ?s wdt:P625 ?coordinates .
} INSERT {
  ?s lpviz:hasAbstraction ?abstraction .

  ?abstraction geo:location ?abstractionLocation .
  
  ?abstractionLocation a geo:SpatialThing ;
      geo:long ?long ;
      geo:lat ?lat .
  
  ?abstraction rdfs:label ?abstractionLabel .
} WHERE {

  ?s wdt:P625 ?coordinates ;
    rdfs:label ?label .

  BIND(CONCAT("Coordinate location of  ", STR(?label)) AS ?abstractionLabel)
  
  BIND(REPLACE(STR(?coordinates), "Point\\(([0-9]{2}\\.[0-9]*) ([0-9]{2}\\.[0-9]*)\\)", "$1") AS ?lat)
  
  BIND(REPLACE(STR(?coordinates), "Point\\(([0-9]{2}\\.[0-9]*) ([0-9]{2}\\.[0-9]*)\\)", "$2") AS ?long)
  
  BIND(IRI(CONCAT(STR(?s), "/abstraction/wikidata-coordinate-location2geo-SpatialThing")) AS ?abstraction)
  BIND(IRI(CONCAT(STR(?s), "/abstraction/wikidata-coordinate-location2geo-SpatialThing/location")) AS ?abstractionLocation)
  
}
