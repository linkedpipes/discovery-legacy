PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX lpviz: <http://visualization.linkedpipes.com/ontology/>

DELETE {
  ?s dct:issued ?dateTime .
} INSERT {
  ?s lpviz:hasAbstraction ?abstraction .
  
  ?abstraction a time:Instant ;
    time:inXSDDateTime ?dateTime ;
	rdfs:label ?abstractionLabel .
} WHERE {
  ?s dct:issued ?dateTime .
  
  OPTIONAL {
	?s dct:title ?title .
	 BIND(CONCAT("Issuance of ", STR(?title)) AS ?abstractionLabel)
  }
  
  BIND(IRI(CONCAT(STR(?s), "/abstraction/dct-issued2time-Instant")) AS ?abstraction)
}