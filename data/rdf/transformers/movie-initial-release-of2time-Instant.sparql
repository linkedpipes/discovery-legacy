PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX movie: <http://data.linkedmdb.org/resource/movie/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX lpviz: <http://visualization.linkedpipes.com/ontology/>

DELETE {
  ?m movie:initial_release_date ?date ;
    dct:date ?date .
} INSERT {
  ?m lpviz:hasAbstraction ?abstraction .
  
  ?abstraction a time:Instant ;
    time:inXSDDateTime ?date ;
	rdfs:label ?abstractionLabel .
} WHERE {
  ?m movie:initial_release_date ?date .
  
  OPTIONAL {
	?m dct:title ?title .
	 BIND(CONCAT("Initial release date of ", STR(?title)) AS ?abstractionLabel)
  }
  
  OPTIONAL {
    ?m dct:date ?date .
  }
  
  BIND(IRI(CONCAT(STR(?m), "/abstraction/linkedmdb-initial-release-of2time-Instant")) AS ?abstraction)
}
